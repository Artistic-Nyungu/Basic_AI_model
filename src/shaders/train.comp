#version 460 core
#extension GL_ARB_compute_shader : require

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

struct Range {
    int begin;
    int end;
};

struct ForwardingLayer {
    Range neurons;
    Range weights;
    Range biases;
    int srcNeurons;
    int dstNeurons;
};

layout(std430, binding = 0) buffer NeuronsBuffer { float neurons[]; };
layout(std430, binding = 1) buffer WeightsBuffer { float weights[]; };
layout(std430, binding = 2) buffer BiasesBuffer { float biases[]; };
layout(std430, binding = 3) buffer ForwardingLayersBuffer { ForwardingLayer layers[]; };

uniform int layerIdx;

void main() {
    int neuronIdx = int(gl_GlobalInvocationID.x);

    ForwardingLayer layer = layers[layerIdx];
    if (neuronIdx >= layer.dstNeurons) return;

    int dstNeuronIdx = layer.neurons.begin + neuronIdx;
    float sum = 0.0f;

    // Previous layer's neurons start at current begin minus srcNeurons
    int prevLayerStart = layer.neurons.begin - layer.srcNeurons;
    for (int i = 0; i < layer.srcNeurons; i++) {
        int srcNeuronIdx = prevLayerStart + i;
        int weightIdx = layer.weights.begin + neuronIdx * layer.srcNeurons + i;
        sum += neurons[srcNeuronIdx] * weights[weightIdx];
    }

    sum += biases[layer.biases.begin + neuronIdx];
    neurons[dstNeuronIdx] = log(1.0f + exp(sum)); // Softplus activation
}